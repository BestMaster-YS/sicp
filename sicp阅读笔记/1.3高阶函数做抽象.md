# 1.3.1 过程作为抽象

```scheme
(define (sum-integers a b)
  (if (> a b)
      0
      (+ a (sum-integers (+ a 1) b)))
)
(define (sum-cubes a b)
  (if (> a b)
      0
      (+ (cube a) (sum-cubes (+ a 1) b)))
)
(define (pi-sum a b)
  (if (> a b)
      0
      (+ (/ 1.0 (* a (+ a 2))) (pi-sum (+ a 4) b)))
)
```

上面三个过程共享一种公共的基础模式，不同在地方在于过程名称，累计的当前项计算的结果，以及下个项的基础值函数。

```scheme
(define (<name> a b)
	(if (> a b)
			0
      (+ (<term> a)
         (<name> (<next> a) b)))
)
```

将基础模式化为抽象过程

```scheme
(define (sum term a next b)
  (if (> a b)
      0
      (+ (term a)
         (sum term (next a) next b)))
)
```

## 1.29 练习

```scheme
(load "util.scm")

(define (simpson-integral f a b n)
  (define h (/ (- b a) n))
  (define (y k) (f (+ a (* h k))))
  (define (term i)
    (cond ((or (= i 0) (= i n)) (y i))
          ((odd? i) (* 4 (y i)))
          ((even? i) (* 2 (y i))))
  )
  (* (/ h 3) (sum term 0 inc n))
)

(simpson-integral cube 0 1 1000)
```

## 1.30练习

```scheme
(define (sum term a next b)
  (define (iter a result)
    (if (> a b)
        result
        (iter (next a) (+ result (term a))))
  )
  (iter a 0)
)
```

## 1.31 练习

```scheme
(load "util.scm")

(define (product term a next b)
  (if (> a b)
      1
      (* (term a)
         (product term (next a) next b)))
)

(define (product term a next b)
  (define (iter a result)
    (if (> a b)
        result
        (iter (next a) (* (term a) result)))
  )
  (iter a 1)
)

(define (factrial a b)
  (product identity a inc b)
)

(factrial 1 5)

(define (pi-product n)
  (define (term i)
    (if (odd? i)
        (/ (+ i 1) (+ i 2))
        (/ (+ i 2) (+ i 1)))
  )
  (* 4.0 (product term 1 inc n))
)

(pi-product 10000)
```

## 1.32 练习

```scheme
(define (accumulate combiner null-value term a next b)
  (if (> a b)
      null-value
      (combiner (term a) (accumulate combiner null-value term (next a) next b)))
)

(define (accumulate combiner null-value term a next b)
  (define (iter a result)
          (if (> a b)
              result
              (iter (next a) (combiner (term a) result))))
  (iter a null-value)
)
```

## 1.33 练习

```scheme
(load "util.scm")
(define (filter-accumulate filter combiner null-value term a next b)
  (cond ((> a b) null-value)
        ((filter a) (combiner (term a) (filter-accumulate filter
                                                          combiner
                                                          null-value
                                                          term
                                                          (next a)
                                                          next
                                                          b)))
        (else (filter-accumulate filter combiner null-value term (next a) next b)))
)

(define (sum-prime a b)
  (filter-accumulate prime? + 0 identity a inc b)
)

(sum-prime 2 10)

;;小于 n 的所有与 n 互素的正整数

(define (sum-prime-each n)
  (define (prime-each? i)
    (= (gcd i n) 1)
  )
  (filter-accumulate prime-each? + 0 identity 1 inc n)
)

(sum-prime-each 10)
```

# 1.3.2 用 lambda 构造过程

Lambda  定义

(lambda (<former-parameter>) <body> )

### 用 let 创建局部变量

1. let 使人们尽可能的在最接近使用的地方建立局部变量约束。
2. 变量的值是在 let 之外计算。即用 let 创建多个变量时，变量之间不能相互引用影响。

# 1.3.3 过程作为一般性的方法

## 通过区间折半寻找方程的根

```scheme
(define (search f neg-point pos-point)
  (let ((mid-point (average neg-point pos-point)))
    (if (close-enough? neg-point pos-point)
        mid-point
        (let ((test-value (f mid-point)))
          (cond ((positive? test-value)
                 (search f neg-point mid-point))
                ((negative? test-value)
                 (search f mid-point pos-point))
                (else
                  mid-point)))))
)

(define (close-enough? a b)
  (< (abs (- a b)) 0.001)
)

(define (half-interval-method f a b)
  (let ((a-value (f a))
        (b-value (f b)))
    (cond ((and (positive? a-value) (negative? b-value))
           (search f b a))
          ((and (positive? b-value) (negative? a-value))
           (search f a b))
          (else
           (error "Values are not of opposite sign" a b))))
)
```

## 寻找函数的不动点

```scheme
(define tolerance 0.00001)

(define (fix-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance)
  )

  (define (try guess)
    (let ((next (f guess)))
      (if (close-enough? guess next)
          next
          (try next)))
  )
  (try first-guess)
)
```

### 用不动点函数计算 sqrt

$$
y^2 = x \rightarrow y = \frac{x}{y} \\
y = \frac{x}{y} \equiv y = \frac{1}{2}\cdot(\frac{x}{y} + y) 
$$



然而该不动点函数并不收敛，考虑 y1, 则下一个则是 x/y1, 下下个则右回到 y1。为了控制震荡，可以将下一个猜测值改为 y 和 x/y 的平均值。

```scheme
(define (sqrt x)
	(fix-point (lambda (y) (/ 2 (+ y (/ x y)))))  
)
```

 ## 1.35 练习

$$
\Phi^2 = \Phi + 1 \rightarrow \Phi = \frac{1}{\Phi} + 1
$$

```scheme
(fix-point (lambda (x) (+ 1 (/ 1 x))) 1.0)
;; Value: 1.6180327868852458
```

## 1.36 练习

```scheme
(load "util.scm")

(define (fix-point f first-guess)
  (define (close-enough? v1 v2)
    (< (abs (- v1 v2)) tolerance)
  )

  (define (try guess)
    (let ((next (f guess)))
      (newline)
      (display next)
      (if (close-enough? guess next)
          next
          (try next)))
  )
  (newline)
  (display first-guess)
  (try first-guess)
)

(fix-point (lambda (x) (/ (log 1000) (log x)))
           2)

2
9.965784284662087
3.004472209841214
6.279195757507157
3.759850702401539
5.215843784925895
4.182207192401397
4.8277650983445906
4.387593384662677
4.671250085763899
4.481403616895052
4.6053657460929
4.5230849678718865
4.577114682047341
4.541382480151454
4.564903245230833
4.549372679303342
4.559606491913287
4.552853875788271
4.557305529748263
4.554369064436181
4.556305311532999
4.555028263573554
4.555870396702851
4.555315001192079
4.5556812635433275
4.555439715736846
4.555599009998291
4.555493957531389
4.555563237292884
4.555517548417651
4.555547679306398
4.555527808516254
4.555540912917957
4.555532270803653
Value: 4.555532270803653

(fix-point (lambda (x) (average (log x) (/ (log 1000) (log x))))
           2)

2
5.329465732611016
2.9007973697165856
3.7756141082831407
3.2639907710046643
3.5111894598052835
3.3779788470705414
3.446017666307582
3.4102551700687442
3.4287782234391004
3.4191099850457123
3.4241362471641277
3.421517768433306
3.422880410388422
3.4221708981226486
3.4225402244953282
3.4223479475372502
3.4224480418826486
3.4223959332270235
3.4224230601672776
3.422408938153278
3.4224162898895893
Value: 3.4224162898895893
```

## 1.37 练习

```scheme
(load "util.scm")

(define (cont-frac n d k)
  (define (cont-frac-rec i)
          (if (= k i)
              (/ (n i) (d i))
              (/ (n i) (+ (d i) (cont-frac-rec (+ i 1)))))
  )
  (cont-frac-rec 1)
)

(define (cont-frac n d k)
  (define (iter i prev)
    (if (= i 1)
        (/ (n i) (+ (d i) prev))
        (iter (- i 1) (/ (n i) (+ (d i) prev))))
  )
  (iter (- k 1) (/ (n k) (d k)))
)

(fix-point (lambda (x) (+ 1 (/ 1 x))) 1.0)
;Value: 1.6180327868852458
(/ 1.0 (cont-frac (lambda (i) 1.0)
                  (lambda (i) 1.0)
                  10))
;Value: 1.6181818181818184
(/ 1.0 (cont-frac (lambda (i) 1.0)
                  (lambda (i) 1.0)
                  100))
;Value: 1.618033988749895
(/ 1.0 (cont-frac (lambda (i) 1.0)
                  (lambda (i) 1.0)
                  1000))
;Value: 1.618033988749895
```

## 1.38 练习

$$
D_i = 
\begin{cases}
\frac{(i + 1)}{3} \cdot 2  & (i+1) \mod 3 = 0 \\
1
\end{cases}
$$

```scheme
(define (d i)
  (if (= (remainder (+ i 1) 3) 0)
      (* 2 (/ (+ i 1) 3))
      1)
)
(define (e k)
  (+ 2.0
     (cont-frac (lambda (i) 1)
                d
                k))
)
(e 100)
;Value: 2.718281828459045
```

## 1.39 练习

```scheme
(load "util.scm")

(define (tan-cf x k)
  (cont-frac (lambda (i)
               (if (= i 1)
                   x
                   (- (square x))))
             (lambda (n) (- (* 2 n) 1))
             k)
)

(tan-cf 10.0 100)
(tan 10)
```

# 1.3.4 过程作为返回值

将不动点与平均阻尼组合

```scheme
(define (average-damp f)
  (lambda (x) (average x (f x)))
)
(define (sqrt x)
  (fix-point (average-damp (lambda (y) (/ x y))) 1.0)
)

(sqrt 10)

(define (cube-root x)
  (fix-point (average-damp (lambda (y) (/ x (square y)))) 1.0)
)

(cube-root 27)
```

## 牛顿法

$$
\text{当 $x \mapsto g(x)$ 是可微函数时，则方程 $g(x)=0$ 的一个解就是函数 $ x \mapsto f(x)$的一个不动点} \\
f(x) = x - \frac{g(x)}{Dg(x)} \\
\text{Dg(x)是 g 函数对 x 的导数} \\
Dg(x) = \frac{g(x+dx)-g(x)}{dx}
$$

```scheme
(define dx 0.00000001)

(define (deriv g)
	(lambda (x) (/ (- (g (+ x dx))
                    (g x))
                 dx))
)

(define (newton-transform g)
	(lambda (x) (- x
                 (/ (g x)
                    ((deriv g) x))))  
)

(define (newton-method g guess)
	(fix-point (newton-transform g) guess)  
)
```

## 抽象和第一级过程

```scheme
(define (fixed-point-of-transform g transform guess)
  (fix-point (transform g) guess)
)
```

### 第一级元素特征

1. 可以用变量命名
2. 可以提供给过程作为参数
3. 可以由过程作为结果返回
4. 可以包含在数据结构中

## 1.40 1.41 1.42 1.43 练习

```scheme
;1.40
(define (cubic a b c)
  (lambda (x) (+ (cube x)
                 (* a (square x))
                 (* b x)
                 c))
)

(define (cubic-root a b c)
  (newton-method (cubic a b c) 1.0)
)

(cubic-root 1 1 1)
;1.41
(define (double f)
  (lambda (x) (f (f x)))
)
;1.42
(define (compose f g)
  (lambda (x) (f (g x)))
)
;1.43
(define (repeat f n)
  (if (= n 1)
      (lambda (x) (f x))
      (compose (repeat f (- n 1)) f))
)
```

## 1.44 练习

```scheme
(define (smooth f)
  (lambda (x) (/ (+ (f x) (f (+ x dx)) (f (- x dx)))
                 3))
)

(define (smooth-repeat f n)
  (lambda (x) (repeat (smooth f) n))
)
```

## 1.45 练习

```scheme
(load "util.scm")

;;

;; 求幂 fast-expt 需处理 n = 0 的情形
(define (expt base n)
  (if (= n 0)
      1
      (fast-expt base n))
)

(expt 2 5)

;; 平均阻尼 repeat
(define (repeat-average-damp n)
  (repeat average-damp n)
)

(define (damped-nth-root n times-damp)
  (lambda (x)
          (fixed-point-of-transform (lambda (y) (/ x (expt y (- n 1))))
                                    (repeat-average-damp times-damp)
                                    1.0))
)

(define (test nth times)
  ((damped-nth-root nth times) (expt 2 nth))
)
;; nth  damp-times
;; 2    1
(test 2 1)

;; 3    1
(test 3 1)

;; 4    2
(test 4 2)

;; 5    2
(test 5 2)

;; 6    2
(test 6 2)

;; 7    2
(test 6 2)

;; 8    3
(test 8 3)

;; 9    3
(test 9 3)

(define (lg n)
  (cond ((> (/ n 2) 1)
         (+ (lg (/ n 2)) 1))
        ((< (/ n 2) 1)
         0)
        (else 1))
)

(define (nth-root n)
  (damped-nth-root n (lg n))
)

((nth-root 10) 1024)
```

## 1.46

```scheme
(define (interative-improve good-enough? improve)
  (define (improve-iter x)
    (if (good-enough? x)
        x
        (improve-iter (improve x)))
  )
  (lambda (x) (improve-iter x))
)

(define (interative-improve good-enough? improve)
  (lambda (x)
          (if (good-enough? x)
              x)
              ((interative-improve good-enough? improve) (improve x)))
)
```












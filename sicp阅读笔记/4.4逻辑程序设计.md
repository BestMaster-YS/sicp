



## 演绎信息检索

### 简单查询

```scheme
;; 数据

(address (Bitdiddle ben) (Slumerville (Ridge Road) 10))
(job (Bitdiddle ben) (computer wizard))
(salary (Bitdiddle ben) 60000)

(address (Hacker Alyssa P) (Cambridge (Mass Ave) 78))
(job (Hacker Alyssa P) (computer programmer))
(salary (Hacker Alyssa P) 40000)
(supervisor (Hacker Alyssa P) (Bitdiddle Ben))

(address (Fect Cy D) (Cambridge (Ames Street) 3))
(job (Fect Cy D) (computer programmer))
(salary (Fect Cy D) 35000)
(supervisor (Fect Cy D) (Bitdiddle Ben))

(address (Tweakit Lem E) (Boston (Bay State Road) 22))
(job (Tweakit Lem E) (computer technician))
(salary (Tweakit Lem E) 25000)
(supervisor (Tweakit Lem E) (Bitdiddle Ben))

(address (Reasoner Louis) (Slumerville (Pine Tree Road) 80))
(job (Reasoner Louis) (computer programmer trainee))
(salary (Reasoner Louis) 30000)
(supervisor (Reasoner Louis) (Hacker Alyssa P))

(supervisor (Bitdiddle Ben) (Warbucks Oliver))
(address (Warbucks Oliver) (Swellesley (Top Heap Road)))
(job (Warbucks Oliver) (administration big wheel))
(salary (Warbucks Oliver) 150000)

(address (Scrooge Eben) (Weston (Shady Lane) 10))
(job (Scrooge Eben) (accounting chief accountant))
(salary (Scrooge Eben) 75000)
(supervisor (Scrooge Eben) (Warbucks Oliver))

(address (Cratchet Robert) (Allston (N Harvard Street) 16))
(job (Cratchet Robert) (accounting scrivener))
(salary (Cratchet Robert) 18000)
(supervisor (Cratchet Robert) (Scrooge Eben))

(address (Aull DeWitt) (Slumerville (Onion Square) 5))
(job (Aull DeWitt) (administration secretary))
(salary (Aull DeWitt) 25000)
(supervisor (Aull DeWitt) (Warbucks Oliver))

(can-do-job (computer wizard) (computer programmer))
(can-do-job (computer wizard) (computer technician))

(can-do-job (computer programmer) (computer programmer trainee))
(can-do-job (administration secretary) (administration big wheel))
```

查询语法：``(job ?x (computer programmer))`` ，所有数据对该模式进行匹配，该模式由三项组成：1. 文字符号job，2. ?x 代表模式变量，3. 文字表 ``(computer programmer)``

模式变量的一般形式是一个符号，作为变量名。模式里可能不只一个变量。``(address ?x ?y)``

模式中也可以不存在变量，该模式则是去确认该模式是否存在数据库中。同一变量可以在模式中出现多次，且不同位置。

对模式的总结：

* 系统将找出使得查询模式中变量满足这一模式的所有赋值，为这些变量找出所有值的集合
* 系统对查询的响应方式，就是列出查询模式的所有满足要求的实例

### 复合查询

查询语言提供了组合能力（构造逻辑表达式的组合手段：and，not，or），去构造复合操作。

and，or，not 复合查询：``((and | or | not) <query1> <query2> <query3> ...)``

数据库的数据必须对于所有 query 都同时满足。

```scheme
(and (job ?person (computer programmer))
     (address ?person ?where))

;; The resulting output is

(and (job (Hacker Alyssa P) (computer programmer))
     (address (Hacker Alyssa P) (Cambridge (Mass Ave) 78)))
(and (job (Fect Cy D) (computer programmer))
     (address (Fect Cy D) (Cambridge (Ames Street) 3)))
```

复合查询本身也是一种模式，模式可以是对数据进行匹配也是对数据进行展示。

### 规则

规则：查询的抽象

```scheme
;; lives-near 描述的规则为居住在同一个小镇且不同一个人的两个人可以满足该模式匹配
(rule (lives-near ?person1 ?person2)
      (and (address ?person1 (?town . ?rest1))
           (address ?person2 (?town . ?rest2))
           (not (same ?person1 ?person2))))

(rule (same ?x ?x))
```

规则的一般形式：``(rule <conclusion> <body>)``

``<conclusion>`` 为模式，``<body>`` 为任何查询.

在简单查询和复合查询中，模式担任着数据的展示和数据匹配查询工作，

在规则中结论为模式负责展示和规则体负责查询。 

一个规则是一组断言，相应规则的结论的所有实例，其变量赋值满足规则的体。

规则也可以作为其他规则的一部分。



###  将逻辑看做程序

append 操作可以用两条规则刻画

1. 对于任何表y，将空表和y的append形成的是y
2. 对于任何形式的u、v、y和z，将 (cons u v) 与 y append形成 (cons u z)，条件是v与y的append形成z

```scheme
(rule (append-to-form () ?y ?y))
(rule (append-to-form (?u . ?v) ?y (?u . ?z))
      (append-to-form ?v ?y ?z))


;; 用前面的方式写 append-to-form
(rule (append-to-form-data ?x ?y ?z)
      (or (and (same ?x ())
               (same ?y ?z))
          (and (same ?x (?u . ?v))
               (same ?z (?u . ?k))
               (append-to-form-data ?v ?y ?k))))
;; 没有上面方式的简洁，原理相同，递归+递归结束条件
;; 这里隐含着：or 的执行为顺序且是应用序，上面的逻辑规则也是先执行第一条规则，在执行第二条规则

;; 测试
(append-to-form ?x ?y (a b c d))

(append-to-form (a b c d) () (a b c d))
(append-to-form (a b c) (d) (a b c d))
(append-to-form (a b) (c d) (a b c d))
(append-to-form (a) (b c d) (a b c d))
(append-to-form () (a b c d) (a b c d))
```

这里表明了：一条规则可能含有多种的模式和查询。``append-to-form`` 有两个模式和一个查询。

这里声明的规则与前面的规则不同，规则 ``append-to-form`` 展示的好像是更高级的模式匹配，不像前面只是对数据的筛选



## 4.4.2 查询系统如何工作

本节内容主要讲述查询系统中与底层实现细节无关的一般性结构。

查询求值器必须执行某种搜索，以便将有关的查询与数据库里的事实和规则进行匹配。可采用两种方式：

1. 利用 amb 求值器
2. 利用流，去设法控制搜索

查询系统的组织结构围绕两个核心操作：

#### 模式匹配

一个模式匹配器是一个程序，它检查数据项是否符合一个给定的模式。（最小单元）

例子：((a b) c (a b)) 与模式 (?x c ?x) 匹配，其中模式变量?x 约束于(a b)，也可以与 (?x ?y ?x) 匹配，?x 约束于 (a b)，?y 约束于 c。

查询系统所用的模式匹配器以一个模式、一个数据和一个框架作为输入。

框架中包含着模式中对变量约束，框架会不断的获取模式中的变量约束。 

框架描述了模式变量的约束，匹配器检查该数据是否以某种方式与模式匹配，这种方式又是与框架已有的约束相容。==如果匹配则匹配器就返回原来框架的一个扩充，其中加入了由当前匹配确定的所有新约束==。如果不匹配，就指出该匹配失败。

这个模式匹配器提供了处理不涉及规则的简单查询所需的所有机制。匹配成功返回的框架中有对应模式变量绑定的值，用对应的值实例化该模式。



- 框架的流

  ![image-20201220215037282](/home/bestmasterliubin/dev/sicp/sicp阅读笔记/4.4逻辑程序设计.assets/image-20201220215037282.png)

这里的含义为 zip, 输入的框架流与数据库中的断言流一一对应，输出流中每一个的框架都是对应输入流中的框架增加了断言流中的模式变量的约束，如上图中含有 ?x，?y 的约束，若是该输入流中框架对于新增的约束不兼容，则会返回失败，进行过滤。

- 复合查询

对于复合查询的处理，利用匹配器带着特定框架去探查匹配的能力。

```scheme
(and (can-do-job ?x (computer programmer trainee))
     (job ?person ?x))
```

首先对 ``(can-do-job ?x (computer programmer trainee))`` 进行匹配，产生一个框架流，其中每个框架都包含一个对于 ?x 的约束。然后对于流中的每个框架以 ``(job ?person ?x)`` 模式进行匹配，并且对已给定的 ?x 的约束相容。

![image-20201220220541302](/home/bestmasterliubin/dev/sicp/sicp阅读笔记/4.4逻辑程序设计.assets/image-20201220220541302.png)

![image-20201220220648186](/home/bestmasterliubin/dev/sicp/sicp阅读笔记/4.4逻辑程序设计.assets/image-20201220220648186.png)



<span style="color: red;">复合操作的处理很慢</span>

从框架流的观点看，查询的not则是过滤器，要求删除所有满足该查询的框架。

#### 合一

通过规则的结论部分和给定的查询模式，找到所有用到的规则。规则的结论部分很像断言，但含有变量，也可能不是数据库中数据表的形式。

为处理规则结论带有变量的情况，就需要模式匹配的推广（合一），其中的“模式”和“数据”都可以包含变量。合一器取两个都可以包含常量和变量的模式为参数，设法去确定能否找到对其中变量的某种赋值，使两模式相等。

查询系统中合一器与模式匹配器相同，以一个框架作为输入，执行与该框架相容的合一工作。合一算法使查询系统中最难的部分。

例子：

```scheme
;;合一
(?x ?x) ((a ?y c) (a b ?z))
;; 可以看成
;; ?x = (a ?y c)
;; ?x = (a b ?z)
;; (a ?y c) = (a b ?z)
;; a = a, ?y = b, c = ?z
;;

(?x a) ((b ?y) ?z)
;; ?x = (b ?y), a = ?z, 无法确定 x, y 的值，在这个框架中不存在对 ?y 的约束
;; 存在对 ?x 的约束，?x 满足 ?x = (b ?y)

```



#### 规则的应用

例子：``(lives-near ?x (Hacker Alyssa P))``

1. 首先采用常规模式匹配过程，查看数据库中是否存在任何与这个模式相匹配的断言。
2. 若没有，则将查询模式与每条规则的结论进行合一，找到相对应的规则

```scheme
(rule (lives-near ?person1 ?person2)
      (and (address ?person1 (?town . ?rest1))
           (address ?person2 (?town . ?rest2))
           (not (same ?person-1 ?person-2))))
```

进行合一后，返回一个框架，其中 ?person2 约束到 (Hacker Alyssa P)，?x 约束到 ?person1.

3. 对得到的框架，去求值有这一规则的体给定的复合查询。成功的匹配将扩展原有框架，提供对          ?person1 的约束，?x 的约束。
4. 用框架中的约束实例化初始的查询模式。



一般而言，当查询求值器试图在一个描述了某些模式变量匹配的框架里，完成对一个查询模式的匹配时，它将采用以下方法去设法应用一条规则：

1. 将查询与规则的结论做合一，以便扩展原有框架
2. 相对于扩展后的框架，去求值由规则体形成的查询
3. 将该过程的形式参数约束于实际参数，已形成一个框架去扩充原来的过程环境。
4. 相对于扩充后的环境，去求值由过程体形成的表达式

#### 简单查询

通过使用规则和断言去求值简单查询：

给定一个查询模式和一个框架的流，对输入流里的每个框架产生两个流：

- 一个扩充框架的流。得到扩充框架的方式是用模式匹配器，拿给定的模式与数据库里的所有断言做匹配。
- 另一个扩充框架的流，通过应用所有可能的规则而得到（用合一器）。

将两个流链接到一起产生一个新流，其中包含与原框架相容的，能满足给定模式的所有不同方式。

#### 查询求值器和驱动循环

协调各种匹配操作的过程称为：``qeval`` ，以一个框架流和一个查询为输入，输出为一个框架的流，对应于查询模式的所有成功匹配，其中的框架都是输入流里框架的扩充。与 eval 类似，qeval 根据表达式查询的不同类型对它们工作进行分派。包括特殊形式(and，or，not和lisp-value)的过程，以及简单查询的过程。

驱动循环则是从终端读入查询，对于每一个查询，都用一个包含一个空框架的流调用qeval。调用产生出所有可能匹配的流。对于流中的每个框架，驱动循环用该框架约束的值实例原来的查询并进行打印。

驱动循环中还要检查特殊命令 assert!，用于指明一个输入并不是查询，而是断言或规则，应加入数据库中。

```scheme
(assert! (job (Bitdiddle Ben) (computer wizard)))

(assert! (rule (wheel ?person)
               (and (supervisor ?middle-manager ?person)
                    (supervisor ?x ?middle-manager))))
```



## 4.4.3 逻辑程序设计是数理逻辑吗？

查询语言中各种组合手段与数理逻辑中的操作类似，而且查询语言规则的应用，就是通过正当的推理方法完成。查询语句与数理逻辑并不相同，查询语句中还提供了一种控制结构，它采用过程性的方式来解释逻辑语句。

```scheme
(and (job ?x (computer programmer))
     (supervisor ?x ?y))
;; 因为工程师和上司的数量不同，两者执行的效率也会不同
(and (supervisor ?x ?y)
     (job ?x (computer programmer)))
```

![image-20201223224324938](/home/bestmasterliubin/dev/sicp/sicp阅读笔记/4.4逻辑程序设计.assets/image-20201223224324938.png)

查询语言只是数理逻辑的一个可以用过程方式去解释的子集。

断言代表一个简单事实（一个原子命题）；一条规则表示一个蕴含，所有使规则的体成立的情况，也能使规则的结论成立。规则有一种很自然的过程性解释：为了得到一条规则的结论，请设法得到规则的体。规则也描述了计算。

#### 无穷循环

对于逻辑程序做过程性解释存在一个推论，在解决问题时，有可能构造极端低效的程序，可能会陷入无限循环。

例子：

```scheme
(assert! (married Minnie Mickey))

;; 查询
(married Mickey ?who)
;; 无结果

;; 增加规则
(assert! (rule (married ?x ?y)
               (married ?y ?X)))

;; 查询
(married Mickey ?who)
;; 陷入死循环

```



#### 与 not 有关的问题

```scheme
(and (supervisor ?x ?y)
     (not (job ?x (computer programmer))))

(and (not (job ?x (computer programmer)))
     (supervisor ?x ?y))
```

以上两个查询的结果不相同，原因在于我们对 not  的实现，第二个查询返回空框架流，是因为 not 是对原有框架的过滤，而第二个查询对初始框架进行过滤，就导致全部被过滤掉。

另一个方面，在逻辑中，"非P" 表示为 P 不真，而在查询系统中，"非P" 表示不能由数据库里的数据得到，隐含条件是数据库理论上包含所有的数据。

## 4.4.4 查询系统的实现

#### 4.4.4.1 驱动循环和实例化

驱动循环不停止的读取输入表达式

1. exp -> 数据库的规则或断言 -> 加载到数据库中

2. exp -> 查询 -> 将查询和初始的框架流（包含一个空框架）发送给求值器 ``qeval`` -> 求值结果为一个框架流，根据从数据库找到的满足查询的变量值生成。

在对一个输入表达式进行任何处理之前，驱动循环都已语法方式将其变换到另一种更容易有效处理的形式。其中涉及到修改模式变量的表示。这一步骤由 ``query-syntax-process`` 完成

在对查询进行实例化时，所有仍未被约束的变量都需要在打印之前变换会原来的输入表示形式。这一步骤由 ``contract-question-mark`` 完成。

```scheme

(define input-prompt "::: Query input:")
(define output-prompt "::: Query results:")


(define (query-driver-loop)
  (prompt-for-input input-prompt)
  (let ((q (query-syntax-process (read))))
           ;; 判断是否为断言
    (cond ((assertion-to-be-added? q)
           (add-rule-or-assertion! (add-assertion-body q))
           (newline)
           (display "Assertion added to data base.")
           (query-driver-loop))
          (else
           ;; 查询
           (newline)
           (display output-prompt)
           (display-stream
            (stream-map
             ;; 实例化
              (lambda (frame)
                (instantiate q
                             frame
                             (lambda (v f)
                               (constract-questopn-mark v))))
              ;; 初始空框架
              (qeval q (singleton-stream '()))))
           (query-driver-loop)))))

```

实例化一个表达式，首先进行复制，并用给定框架的值取代表达式里相应的变量。可能值本身也可能需要实例化（包含变量）。当变量不能实例化时，应执行 ``unbound-var-handler``

```scheme
(define (instantiate exp frame unbound-var-handler)
  (define (copy exp)
    (cond ((var? exp)
           (let ((binding (binding-in-frame exp frame)))
             (if binding
                 (copy (binding-value binding))
                 (unbound-var-handler exp frame))))
          ((pair? exp)
           (cons (copy (car exp)) (copy (cdr exp))))
          (else exp)))
  (copy exp))
```

#### 4.4.4.2 求值器

求值器时查询的基本过程，以一个查询和一个框架的流为输入，返回被扩充的框架的流。

``qeval`` 通过 get，put识别各种特殊形式，并完成数据导向的分派。当无法识别为特殊形式的查询都被认为是一种简单查询。

```scheme
(define (qeval query frame-stream)
  (let ((qproc (get (type query) 'qeval)))
    (if qproc
        (qproc (contents query) frame-stream)
        (simple-query query frame-stream))))
```

##### 简单查询

```scheme
(define (simple-query query-pattern frame-stream)
  (stream-flatmap
   (lambda (frame)
     (stream-append-delayed
      (find-assertions query-pattern frame)
      (delay (apply-rules query-pattern frame))))
   frame-stream))
```

通过输入流中的每个框架，用 ``find-assertions`` 去做模式和数据库里所有断言的匹配，生成一个扩充框架的流。通过 ``apply-rules`` 应用所有可能的规则，生成出另一个扩充框架的流。然后将两个流组合成为一个 ``stream-append-delayed``，表示满足给定模式，而且与开始框架相同的所有不同方式。用 ``stream-flatmap`` 组合起处理每个输入框架而产生的结果流。

##### 复合查询

```scheme
(define (conjoin conjuncts frame-stream)
  (if (empty-conjunctions? conjuncts)
      frame-stream
      (conjoin (rest-conjuncts conjuncts)
               ;; 递归进行查询并返回符合条件的扩充后的框架流
               (qeval (first-conjunct conjuncts)
                      frame-stream))))

(put 'and 'qeval conjoin)

(define (disjoin disjuncts frame-stream)
  (if (empty-conjunctions? disjuncts)
      the-empty-stream
      (interleave-delayed
       (qeval (first-disjunct disjuncts) frame-stream)
       (delay (disjoin (rest-disjuncts disjuncts)
                       frame-stream)))))

(put 'or 'qeval disjoin)
```

#### 过滤器

```scheme
;; not
(define (negate operands frame-stream)
  (steam-flatmap
   (lambda (frame)
     (if (stream-null? (qeavl (negated-query operands)
                              (singleton-stream frame)))
         (singleton-stream frame)
         the-empty-stream))
   frame-stream))

(put 'not 'qeavl negate)

;; lisp-value
(define (lisp-value call frame-stream)
  (stream-flatmap
   (lambda (frame)
     (if (execute
          (instantiate
           call
           frame
           (lambda (v f)
             (error "Unknown pat var -- LISP-VALUE"))))
         (singleton-stream frame)
         the-empty-stream))
   frame-stream))

(put 'lisp-value 'qeval lisp-value)

;; execute 将谓词应用于对应的参数。
(define (execute exp)
  (apply (eval (predicate exp) user-initial-environment)
         (args exp)))

;; 描述总能满足的查询
(define (always-true ignore frame-stream) frame-stream)
(put 'always-true 'qeval always-true)


```

### 4.4.4.3 通过模式匹配找出断言

``find-assertions`` 以一个模式和一个框架作为输入，返回一个框架的流吗，其中每个框架都是由某个给定框架，经过对给定模式与数据的匹配扩充得到的。

```scheme
(define (find-assertions pattern frame)
  (stream-flatmap (lambda (datum)
                    (check-an-assertion datum pattern frame))
                  (fetch-assertion pattern frame)))
```

这个过程通过 ``fetch-assertions`` 获取数据库中所有断言的流，然后用 ``check-an-assertion`` 判断该模式与断言是否匹配，若成功则返回包含扩充框架的单元素流，失败则返回空流。

```scheme
(define (check-an-assertion assertion query-pat query-frame)
  (let ((match-result
         (pattern-match query-pat assertion query-frame)))
    (if (eq? match-result 'failed)
        the-empty-stream
        (singleton-stream match-result))))
```

匹配器的思路是对照模式检查数据，递归进行，在此同时积累起各个模式变量的约束。如果模式与数据对象相同，则匹配成功，返回已积累的约束形成的框架，否则如果模式是变量则扩充当前框架，将变量与数据的约束加入框架中。条件是约束与当前已有的约束相容。

```scheme
(define (pattern-match pat dat frame)
  (cond ((eq? frame 'failed) 'failed)
        ((equal? pat dat) frame)
        ((var? pat) (extend-if-consistent pat dat frame))
        ((and (pair? pat) (pair? dat))
         (pattern-match (cdr pat)
                        (cdr dat)
                        (pattern-match (car pat)
                                       (car dat)
                                       frame)))
        (else 'failed)))
```

![image-20210104235131218](4.4%E9%80%BB%E8%BE%91%E7%A8%8B%E5%BA%8F%E8%AE%BE%E8%AE%A1.assets/image-20210104235131218.png)

```scheme
(define (extend-if-consistent var dat frame)
  (let ((binding (binding-in-frame var frame)))
    (if binding
        (pattern-match (binding-value binding) dat frame)
        (extend var dat frame))))
```

#### 具有带点尾部的模式

虽然没处理尾部模式，但是查询系统依然能正常工作。因为 ``query-driver-loop`` 使用 Lisp 的 read 基本过程读入查询，并将它表示为一个表结构，其中的圆点将用一种特殊方式处理。

### 4.4.4.4 规则与合一

处理规则与``find-assertions``类似，以一个模式和一个框架作为输入，生成一个通过应用来自数据库的规则而扩充的框架流。

```scheme
;; apply-rules : pattern -> frame -> Stream frame
(define (apply-rules pattern frame)
  (stream-flatmap (lambda (rule)
                    (apply-a-rule rule pattern frame))
                  (fetch-rules pattern frame)))

```

``apply-a-rule`` 首先在给定框架里对规则的结论和模式做合一，扩充自己的实参框架，得到新框架里求值规则的体。对于不同规则应用中变量的名字不能相互干扰。需要生成唯一的标识符。

```scheme
(define (apply-a-rule rule query-pattern query-frame)
  (let ((clean-rule (rename-variables-in rule)))
    (let ((unify-result
           (unify-match query-pattern
                        (conclusion clean-rule)
                        query-frame)))
      (if (eq? unify-result 'failed)
          the-empty-stream
          (qeval (rule-body clean-rule)
                 (singleton-stream unify-result))))))


(define (rename-variables-in rule)
  (let ((rule-application-id (new-rule-application-id)))
    (define (tree-walk exp)
      (cond ((var? exp)
             (make-new-varible exp rule-application-id))
            ((pair? exp)
             (cons (tree-walk (car exp))
                   (tree-walk (cdr exp))))
            (else exp)))
    (tree-walk rule)))
```

合一算法：以两个模式和一个框架为参数，返回扩充后的框架或符号 ``failed``

```scheme
(define (unify-match p1 p2 frame)
  (cond ((eq? frame 'failed) 'failed)
        ((equal? p1 p2) frame)
        ;; 将两个模式中的变量相互约束
        ((var? p1) (extend-if-possible p1 p2 frame))
        ((var? p2) (extend-if-possible p2 p1 frame))
        ((and (pair? p1) (pair? p2))
         (unify-result (cdr p1)
                       (cdr p2)
                       (unify-match (car p1)
                                    (car p2)
                                    frame)))
        (else 'failed)))
```



```scheme
(define (extend-if-possible var val frame)
  (let ((binding (binding-in-frame var frame)))
    (cond (binding
           (unify-match
            (binding-value binding) var frame))
          ((var? val)
           (let ((binding (binding-in-frame val frame)))
             (if binding
                 (unify-match
                  var (binding-value binding) frame)
                 (extend var val frame))))
          ((depends-on? val var frame)
           'failed)
          (else (extend var val frame)))))

;; depends-on 检查想作为某模式变量的值的表达式是否依赖于这一变量。
;; 当我们合一 (?x ?x) (?y (f ?y)) 显然 ?x = ?y = (f ?y)
(define (depends-on? exp var frame)
  (define (tree-walk e)
    (cond ((var? e)
           ;; 检查 var 与 e 是否相等
           (if (equal? var e)
               true
               ;; 检查在frame框架中是否存在 e 的约束进行 depends-on?
               (let ((b (binding-in-frame e frame)))
                 (if b
                     (tree-walk (binding-value b))
                     false))))
          ((pair? e)
           (or (tree-walk (car e))
               (tree-walk (cdr e))))
          (else false)))
  (tree-walk exp))
```

### 4.4.4.5 数据库的维护

在设计逻辑程序设计语言时，一个重要的问题是在检查一个给定模式时，必须考察的无关数据库条目越少越好。可以通过 car 部分是否为常量符号进行初步筛选。

```scheme
(define THE-ASSERTIONS the-empty-stream)

(define (fetch-assertions pattern frame)
  (if (use-index? pattern)
      (get-indexed-assertions pattern)
      (get-all-assertions)))

(define (get-all-assertions) THE-ASSERTIONS)

(define (get-indexed-assertions pattern)
  (get-stream (index-key-of pattern) 'assertion-stream))

(define (get-stream key1 key2)
  (let ((s (get key1 key2)))
    (if s s the-empty-stream)))
```

规则与断言用类似的方法保存，以规则中结论部分的car作为索引。规则与断言不同的是结论部分可以拥有变量。所以对于以变量开始的规则也是匹配的。

```scheme
(define THE-RULES the-empty-stream)

(define (fetch-rules pattern frame)
  (if (use-index? pattern)
      (get-indexed-rules pattern)
      (get-all-rules)))

(define (get-all-rules) THE-RULES)

(define (get-indexed-rules pattern)
  (stream-append
   (get-stream (index-key-of pattern) 'rule-stream)
   (get-stream '? 'rule-stream)))
```

```scheme
(define (add-rule-of-assertion! assertion)
  (if (rule? assertion)
      (add-rule! assertion)
      (add-assertion! assertion)))

(define (add-assertion! assertion)
  (store-asserion-in-index assertion)
  (let ((old-assertions THE-ASSERTIONS))
    (set! THE-ASSERTIONS
          (cons-stream assertion old-assertions))
    'ok))

(define (add-rule! rule)
  (store-rule-in-index rule)
  (let ((old-rules THE-RULES))
    (set! THE-RULES (cons-stream rule old-rules))
    'ok))

(define (store-asserion-in-index assertion)
  (if (indexable? assertion)
      (let ((key (inedx-key-of assertion)))
        (let ((current-assertion-stream
               (get-stream key 'assertion-stream)))
          (put key
               'assertion-stream
               (cons-stream assertion
                            current-assertion-stream))))))

(define (store-rule-in-index rule)
  (let ((pattern (conclusion rule)))
    (if (indexable? pattern)
        (let ((key (index-key-of pattern)))
          (let ((current-rule-stream
                 (get-stream key 'rule-stream)))
            (put key
                 'rule-stream
                 (cons-stream rule
                              current-rule-stream)))))))

(define (indexable? pat)
  (or (constant-symbol? (car pat))
      (var? (car pat))))

(define (use-index? pat)
  (constant-symbol? (car pat)))
```

### 4.4.4.6 流操作



```scheme

(define (stream-append-delayed s1 delayed-s2)
  (if (stream-null? s1)
      (force delayed-s2)
      (cons-stream
       (stream-car s1)
       (stream-append-delayed (stream-cdr s1) delayed-s2))))

(define (interleave-delayed s1 delayed-s2)
  (if (stream-null? s1)
      (force delayed-s2)
      (cons-stream
       (stream-car s1)
       (interleave-delayed (force delayed-s2)
                           (delay (stream-cdr s1))))))

(define (stream-flatmap proc s)
  (flattern-stream (stream-map proc s)))

(define (flattern-stream stream)
  (if (stream-null? stream)
      the-empty-stream
      (interleave-delayed
       (stream-car stream)
       (delay (flattern-stream (stream-cdr stream))))))

(define (singleton-stream x)
  (cons-stream x the-empty-stream))
```

### 4.4.4.7 查询的语法过程

基本工具函数

```scheme
(define (type exp)
  (if (pair? exp)
      (car exp)
      (error "Unknown expression TYPE" exp)))

(define (contents exp)
  (if (pair? exp)
      (cdr exp)
      (error "Unknown expression CONTENTS" exp)))
```

加入数据库的规则和断言

```scheme
(define (assertion-to-be-added? exp)
  (eq? (type exp) 'assert!))

(define (add-assertion-body exp)
  (car (contents exp)))
```

逻辑语法定义

```scheme
(define (empty-conjunctions? exps) (null? exps))
(define (first-conjunct exps) (car exps))
(define (rest-conjuncts exps) (cdr exps))

(define (empty-disjunction? exps) (null? exps))
(define (first-disjunct exps) (car exps))
(define (rest-disjuncts exps) (cdr exps))

(define (negated-query exps) (car exps))
(define (predicate exps) (car exps))
(define (args exps) (cdr exps))
```

规则语法定义

```scheme
(define (rule? statement)
  (tagged-list? statement 'rule))

(define (conclusion rule) (cadr rule))

(define (rule-body rule)
  (if (null? (cddr rule))
      '(always-true)
      (caddr rule)))
```

``query-driver-loop`` 调用 ``query-syntax-process`` 对表达式里的模式变量做一种变换，将其右 ``?symbol`` 形式变换为 ``(? symbol)``，当遇到  ``(job ?x ?y)`` 查询时，在系统内部的实际表示为 ``(job (? x) (? y))``，``(? x)`` 可以直接使用 ``car`` 提取，效率更高

```scheme
(define (query-syntax-process exp)
  (map-over-symbols expand-question-mark exp))

(define (map-over-symbols proc exp)
  (cond ((pair? exp)
         (cons (map-over-symbols proc (car exp))
               (map-over-symbols proc (cdr exp))))
        ((symbol? exp) (proc exp))
        (else exp)))

(define (expand-question-mark symbol)
  (let ((chars (symbol->string symbol)))
    (if (string=? (substring chars 0 1) "?")
        (list '?
              (string->symbol
               (substring chars 1 (string-length chars))))
        symbol)))
```

检测是否为模式变量

```scheme
(define (var? exp)
  (tagged-list? exp '?))

(define (constant-symbol? exp) (symbol? exp))
```

规则应用过程中构造唯一变量

```scheme
(define rule-counter 1)

(define (new-rule-application-id)
  (set! rule-counter (+ 1 rule-counter))
  rule-counter)

(define (make-new-varible var rule-application-id)
  (cons '? (cons rule-application-id (cdr var))))
```

在 ``query-driver-loop`` 打印实例化的查询表达式

```scheme
(define (contract-question-mark variable)
  (string->symbol
   (string-append "?"
                  (if (number? (cadr variable))
                      (string->append (symbol->string (caddr variable))
                                      "-"
                                      (number->string (cadr variable)))
                      (symbol->string (cadr variable))))))
```

### 4.4.4.8 框架与约束

```scheme
(define (make-binding variable value)
  (cons varialbe value))

(define (binding-variable binding)
  (car binding))

(define (binding-value binding)
  (cdr binding))

(define (binding-in-frame variable frame)
  (assoc variable frame))

(define (extend variable value frame)
  (cons (make-binding variable value) frame))
```




























































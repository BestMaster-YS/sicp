# 程序设计的基本元素

## 表达式

在 scheme 中，**486**， **(+ 127 349)** 都称为表达式，后者是将数的表达式组合起来形成符合表达式（组合式），组合式表示一个过程应用，在表达式内最左的元素称为***运算符***，其他元素称为***运算对象***。这种运算形式为前缀表示。

前缀表达式的优点

1.  适用于带有任意个实参的过程

   ```scheme
   (+ 21 35 12 7)
   (* 25 4 12)
   ```

2. 允许直接扩充

   ```scheme
   (+ (* 3 5) (- 10 6))
   ```

## 命名和环境

在 scheme 中，通过**define** 进行事物命名，例如 **(define size 2)**

程序能够将值与符号关联，并且能提取出定义的值，意味着解释器必须维护储存这种名字-值对偶的能力，这种储存称为环境（全局环境）

## 组合式的求值

解释器对于组合式求值的过程

1. 求该组合式个子表达式的值
2. 其他子表达式的值将作为最左子表达式（运算符）的值得那个过程应用于相应的实际参数

处理基本表达式的规则

1. 数的值就是它们所表示的数值
2. 内部运算符的值就是能完成相应操作的机械指令序列
3. 其他名字的值就是环境中关联与这一名字的对象

**环境所扮演的角色就是用于确定表达式中各个符号的意义**

**define** 不是表达式

## 复合过程

过程定义

*(define  (\<name\> \<formal parameters\> )  \<body\> )*



## 过程应用的代换模型

复合过程应用的计算：

将复合过程应用于实际参数，就是在将过程体重的每个形参用相应的实参取代后，对过程体求值

### 正则序

“完全展开而后归约”的求值模型称为 **正则序求值** （先用运算对象表达式去代换形式参数，直至得到一个只包含基本运算符的表达式）

### 应用序

“先求值参数而后应用”的方式称为 **应用序求值** （首先对运算符和各个运算对象求值，而后将得到的过程应用于得到的实际参数）

## 条件表达式和谓词

```scheme
;; 条件表达式的一般性形式
(cond (<p1> <e1>)
      (<p2> <e2>)
      .
      .
      (<pn> <en>))
;; p1 为谓词，指能返回真假的过程或是能求出真假的表达式
(cond (<p1> <e1>)
      (<p2> <e2>)
      .
      .
      (else en))
```

if 表达式    *(if   \<predicate\>   \<consequent\>  \<alternative\>)*

复合运算符

**and,   or,   not**

*(and \<e1\>  \<e2\> ... \<en\>)*  *(or \<e1\>  \<e2\> ... \<en\>)* *(not \<e\> )*

## 实例：采用牛顿法求平方根

牛顿法求平方根：对于 x，起始猜测值为 y = 1，不断的逼近 √x，若是 y² 与 x 的差值精确到一定程度，则求出x的平方根

```scheme
;; 定义求平方根

(define (sqrt-iter guess x)
  (if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x)
                 x)))

;; 平均值
(define (average x y)
  (/ (+ x y) 2))

;; 进一步提高准确性
(define (improve guess x)
  (average guess (/ x guess)))

(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))


(define (sqrt x)
  (sqrt-iter 1.0 x))

(sqrt 2) ;; 1.4142156862745097
```

### 例题：1.6

```scheme
(define (new-if predicate then-clause else-clause)
  (cond (predicate then-clause)
        (else else-clause)))

(define (sqrt-iter guess x)
  (new-if (good-enough? guess x)
      guess
      (sqrt-iter (improve guess x)
                 x)))
```

*因为 new-if 是普通函数，遵循应用序规则，先对参数求值，而在求平方根时对 then-clause 和 else-clause 分别求值，导致造成递归过深，程序报错*

### 例题 1.7

```scheme
(define (good-enough? guess x)
  (< (abs (- (square guess) x)) 0.001))
```

对于 good-enough 函数，在面对较小数时会出错，遇到较大数时会因为精确度问题而爆栈。good-enough 的另一种策略，检查猜测值的变化与猜测值的比率，当比率很小时得到结果。

```scheme
(define (good-enough? guess x)
  (< (/ (abs (- guess (improve guess x))) guess)
     0.001))
```

### 例题 1.8

```scheme
(define (cube x)
  (* x x x))

(define (improve guess x)
  (/ (+ (/ x (square guess)) (* 2 guess))
     3))
;; (x/y^2 + 2y)/3

(define (good-enough? guess x)
  (< (/ (abs (- guess (improve guess x))) guess)
     0.001))

(define (cubic-root-iter guess x)
  (if (good-enough? guess x)
      (improve guess x)
      (cubic-root-iter (improve guess x) x)))

(define (cubic-root x)
  (cubic-root-iter 1.0 x))
```

求出更好的近似值与 sqrt 不一样，其余思想一致 

## 过程作为黑箱抽象

### 局部名

> 过程的意义不应该依赖于其作者为形式参数所选用的名字

过程的形式参数的具体名称不重要，这种称为**约束变量**，一个过程定义约束了它的所有形式参数。将约束变量换名不影响过程的意义。若是变量不被约束则是自由的，过程是在该过程定义里的约束变量的作用越。

### 内部定义和块结构

```scheme
(define (sqrt x)
	(define (good-enough? guess x)
		(< (abs (- (square guess) x)) 0.001))
	(define (improve guess x)
		(average guess (/ x guess)))
	(define (sqrt-iter guess x)
		(if (good-enough? guess x)
				guess
				(sqrt-iter (improve guess x) x)))
	(sqrt-iter 1.0 x)
)
```

在块结构中可以共享自由变量，而不用显示的传递参数。

> 词法作用域要求过程中的自由变量实际引用外围过程定义中所出现的约束。

块结构中的子程序存在词法作用域。







